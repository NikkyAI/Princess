export def get_opt(args: [string], short_op: string, long_op: string, default_: string) -> string {
    let argc = args.size
    for var i in 0..argc {
        let arg = args[i]
        if arg == short_op {
            // -o arg
            if i + 1 < argc {
                return args[i + 1]
            } else {
                print("Must provide an argument to ", short_op, "\n")
                return ""
            }
        } else if starts_with(arg, long_op) {
            // --opt=arg
            let eq = length(long_op)
            if length(arg) > eq {
                if arg[eq] == '=' and arg[eq + 1] != '\0' {
                    arg.value = arg.value ++ (eq + 1)
                    return arg
                } else {
                    print("Illegal argument ", long_op, "\n")
                    return ""
                }
            } else if i + 1 < argc {
                // --opt arg
                return args[i + 1]
            } else {
                print("Must provide an argument to ", long_op, "\n")
                return ""
            }
        }
    }
    return default_
}

export def get_flag(args: [string], short_op: string, long_op: string) -> bool {
    for var i in 0..args.size {
        let arg = args[i]
        if arg == short_op or arg == long_op {
            return true
        }
    }
    return false
}