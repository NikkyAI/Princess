export def get_opt(args: [string], short_op: string, long_op: string, default_: string) -> string {
    let argc = length(args)
    for var i in 0:argc:1 {
        let arg = args[i]
        if arg == short_op {
            // -o arg
            if i + 1 < argc {
                return args[i + 1]
            } else {
                print("Must provide an argument to ", short_op, "\n")
                return null
            }
        } else if starts_with(arg, long_op) {
            // --opt=arg
            let eq = length(long_op)
            if length(arg) > eq {
                if arg[eq] == '=' and arg[eq + 1] != '\0' {
                    return arg ++ (eq + 1)
                } else {
                    print("Illegal argument ", long_op, "\n")
                    return null
                }
            } else if i + 1 < argc {
                // --opt arg
                return args[i + 1]
            } else {
                print("Must provide an argument to ", long_op, "\n")
                return null
            }
        }
    }
    return default_
}

export def get_flag(args: [string], short_op: string, long_op: string) -> bool {
    for var i in 0:length(args):1 {
        let arg = args[i]
        if arg == short_op or arg == long_op {
            return true
        }
    }
    return false
}