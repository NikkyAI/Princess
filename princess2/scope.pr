import map
import typechecking
import parser
import vector
import buffer
import util

export type Value = struct {
    share: parser::ShareMarker
    modifier: parser::VarDecl
    // Name used by the source code
    name: string
    tpe: *typechecking::Type
    value: *
    // In case multiple values share one name (overloaded functions)
    next: *Value
}

export type Scope = struct {
    parent: *Scope
    fields: *map::Map
    // This is a list of imported scopes,
    // for resolving names from other modules without
    // specifying the full path
    imports: *vector::Vector
}

export def enter_scope(scope: *Scope) -> *Scope {
    var s = allocate(Scope)
    (@s).parent = scope
    (@s).fields = map::make()
    (@s).imports = null
    return s
}

export def exit_scope(scope: *Scope) -> *Scope {
    return (@scope).parent
}

export def get(scope: *Scope, id: *parser::Node) -> *Value

def find_function(v: *Value, parameter_t: *vector::Vector, score: *int) -> *Value {
    if not v { return null }
    if not typechecking::is_function((@v).tpe) {
        return null
    }
    var found: *Value = null
    loop {
        let s = typechecking::overload_score((@v).tpe, parameter_t)
        if s >= 0 {
            if s < @score {
                @score = s
                found = v
            }
        }
        v = (@v).next
        if not v { break }
    }
    return found
}

export def get_function(scope: *Scope, id: *parser::Node, parameter_t: *vector::Vector) -> *Value {
    assert((@id).kind == parser::NodeKind::IDENTIFIER)
    if vector::length((@id).value.body) == 1 {
        let name = @(vector::get((@id).value.body, 0) !*string)
        var score = util::MAX_INT32 !int
        var value = find_function(map::get((@scope).fields, name), parameter_t, *score)
        let scores = vector::make()
        if (@scope).imports {
            for var i in 0..vector::length((@scope).imports) {
                let scope2 = vector::get((@scope).imports, i) !*Scope
                var value2 = find_function(map::get((@scope2).fields, name), parameter_t, *score)
                if value2 {
                    value = value2
                    var i = allocate(int)
                    @i = score
                    vector::push(scores, i)
                }
            }
            var found = false
            if vector::length(scores) > 1 {
                // Check if there was an ambiguous reference 
                for var i in 0..vector::length(scores) {
                    var score2 = vector::get(scores, i) !*int
                    if @score2 == score {
                        if found {
                            typechecking::errorn(id, "Ambiguous reference\n")
                            return null
                        }
                        found = true
                    }
                }
            }
        }
        if value { return value }
        if (@scope).parent {
            return get_function((@scope).parent, id, parameter_t)
        }
        return null
    } else {
        let head = vector::head_vec((@id).value.body)
        let tail = vector::tail((@id).value.body)

        let id_head = parser::copy_node(id)
        (@id_head).value.body = head
        let id_tail = parser::copy_node(id)
        (@id_tail).value.body = tail

        let s = get(scope, id_head)
        if s {
            if (@(@s).tpe).kind != typechecking::TypeKind::NAMESPACE {
                typechecking::errorn(id, "Use of :: on non namespace\n")
                return null
            }
            return get_function((@s).value !*Scope, id_tail, parameter_t)
        }
        return null
    }
}

def unwrap_function_without_parameters(v: *Value) -> *Value {
    if not v { return null }
    if not typechecking::is_function((@v).tpe) {
        return v
    }
    loop {
        if vector::length((@(@v).tpe).parameter_t) == 0 {
            return v
        }
        v = (@v).next
        if not v { break }
    }
    return null
}

export def get(scope: *Scope, id: *parser::Node) -> *Value {
    assert((@id).kind == parser::NodeKind::IDENTIFIER)
    if vector::length((@id).value.body) == 1 {
        let name = @(vector::get((@id).value.body, 0) !*string)
        var value = unwrap_function_without_parameters(map::get((@scope).fields, name))
        if value { return value }
        if (@scope).imports {
            for var i in 0..vector::length((@scope).imports) {
                let scope2 = vector::get((@scope).imports, i) !*Scope
                var value2 = unwrap_function_without_parameters(map::get((@scope2).fields, name))
                if value2 and value {
                    // TODO save location of values to give better error messages
                    typechecking::errorn(id, "Ambiguous reference\n")
                    return null
                }
                if value2 {
                    value = value2
                }
            }
        }
        if value { return value }
        if (@scope).parent {
            return get((@scope).parent, id)
        }
        return null
    } else {
        let head = vector::head_vec((@id).value.body)
        let tail = vector::tail((@id).value.body)

        let id_head = parser::copy_node(id)
        (@id_head).value.body = head
        let id_tail = parser::copy_node(id)
        (@id_tail).value.body = tail

        let s = get(scope, id_head)
        if s {
            if (@(@s).tpe).kind != typechecking::TypeKind::NAMESPACE {
                typechecking::errorn(id, "Use of :: on non namespace\n")
                return null
            }
            return get((@s).value !*Scope, id_tail)
        }
        return null
    }
}

export def get_const_value(scope: *Scope, id: *parser::Node) -> *Value {
    let value = get(scope, id)
    if not value {
        typechecking::errorn(id, "Unknown identifier \"")
        error(parser::identifier_to_str(id), "\"\n")
        return null
    }
    assert((@value).modifier == parser::VarDecl::CONST or (@value).modifier == parser::VarDecl::TYPE)
    return value
}

export def get_type(scope: *Scope, id: *parser::Node) -> *typechecking::Type {
    let value = get_const_value(scope, id)
    if not value { return null }
    assert((@value).modifier == parser::VarDecl::TYPE)
    return (@value).value !*typechecking::Type
}

export def enter_namespace(scope: *Scope, node: *parser::Node) -> *Scope

// This function creates the namespaces for identifiers like 
// namespace::path::foo
// beware, this function can return null
def create_path(scope: *Scope, node: *parser::Node) -> *Scope {
    assert((@node).kind == parser::NodeKind::IDENTIFIER)

    let length = vector::length((@node).value.body)
    for var i in 0..length - 1 {
        let ident = vector::get((@node).value.body, i) !*string
        let scope_v = map::get((@scope).fields, @ident) !*Value
        if not scope_v {
            let n = parser::copy_node(node)
            let vec = vector::make()
            vector::push(vec, ident)
            (@n).value.body = vec
            scope = enter_namespace(scope, n)
        } else {
            if (@(@scope_v).tpe).kind != typechecking::TypeKind::NAMESPACE {
                typechecking::errorn(node, "Illegal declaration of ")
                error(parser::identifier_to_str(node), "\n")
                return null
            } else {
                scope = (@scope_v).value !*Scope
            }
        }
    }
    return scope
}

// Returns the last element in a path, eg Element for path::Element
def last_path_element(node: *parser::Node) -> string {
    assert((@node).kind == parser::NodeKind::IDENTIFIER)
    let length = vector::length((@node).value.body)
    return @(vector::get((@node).value.body, length - 1) !*string)
}

// TODO Give better error messages, where was the function/variable declared previously?
// parameter_t is a list of typechecking::NamedParameter
export def create_function(
    scope: *Scope, node: *parser::Node, share: parser::ShareMarker, 
    parameter_t: *vector::Vector, return_t: *vector::Vector) {

    scope = create_path(scope, node)
    if not scope { return }
    let name = last_path_element(node)

    let tpe = typechecking::make_type(typechecking::TypeKind::FUNCTION, node)
    (@tpe).parameter_t = parameter_t
    (@tpe).return_t = return_t
    
    let v = allocate(Value)
    (@v).share = share
    (@v).modifier = parser::VarDecl::CONST
    (@v).name = name
    (@v).tpe = tpe
    (@v).value = null
    (@v).next = null

    if map::contains((@scope).fields, name) {
        var val = map::get((@scope).fields, name) !*Value
        if not typechecking::is_function((@val).tpe) {
            typechecking::errorn(node, "Redeclaration of ")
            error("\"", name, "\"\n")
        }
        var next = (@val).next
        while next {
            if typechecking::overload_score((@next).tpe, (@tpe).parameter_t) == 0 {
                typechecking::errorn(node, "Function ")
                error("\"", name, "\" was already declared previously (same arguments)\n")
            }
            next = (@val).next
        }
        (@next).next = v
    } else {
        map::put((@scope).fields, name, v)
    }
}

export def create_variable(
    scope: *Scope, node: *parser::Node, share: parser::ShareMarker, 
    modifier: parser::VarDecl, tpe: *typechecking::Type, value: *) {
    
    scope = create_path(scope, node)
    if not scope { return }
    let name = last_path_element(node)

    let v = allocate(Value)
    (@v).share = share
    (@v).modifier = modifier
    (@v).name = name
    (@v).tpe = tpe
    (@v).value = value
    (@v).next = null

    if map::contains((@scope).fields, name) {
        typechecking::errorn(node, "Redeclaration of ")
        error("\"", name, "\"\n")
    } else {
        map::put((@scope).fields, name, v)
    }
}

export def create_type(
    scope: *Scope, node: *parser::Node, 
    share: parser::ShareMarker, tpe: *typechecking::Type) {
    
    scope = create_path(scope, node)
    if not scope { return }
    let name = last_path_element(node)
    var type_t = typechecking::make_type(typechecking::TypeKind::TYPE, node)

    let v = allocate(Value)
    (@v).share = share
    (@v).modifier = parser::VarDecl::TYPE
    (@v).name = name
    (@v).tpe = type_t
    (@v).value = tpe
    (@v).next = null

    if map::contains((@scope).fields, name) {
        typechecking::errorn(node, "Redeclaration of ")
        error("\"", name, "\"\n")
    } else {
        map::put((@scope).fields, name, v)
    }
}

export def enter_namespace(scope: *Scope, node: *parser::Node) -> *Scope {
    
    scope = create_path(scope, node)
    if not scope { return null }
    let name = last_path_element(node)
    let tpe = typechecking::make_type(typechecking::TypeKind::NAMESPACE, node)
    let scope2 = enter_scope(scope)

    let v = allocate(Value)
    (@v).share = parser::ShareMarker::NONE
    (@v).modifier = parser::VarDecl::CONST
    (@v).name = name
    (@v).tpe = tpe
    (@v).value = scope2
    (@v).next = null

    if map::contains((@scope).fields, name) {
        typechecking::errorn(node, "Redeclaration of ")
        error("\"", name, "\"\n")
    } else {
        map::put((@scope).fields, name, v)
    }
    return scope2
}