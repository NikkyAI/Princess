
let INITIAL_SIZE = 8

export type Vector = struct {
    length: int
    allocated: int
    data: **
}

def roundup(n: int) -> int {
    if n == 0 {
        return 0
    }
    var r = 1
    while n > r {
        r *= 2
    }
    return r
}

export def make -> *Vector {
    let vec = allocate(Vector)
    (@vec).length = 0
    (@vec).allocated = INITIAL_SIZE
    (@vec).data = allocate((size_of type *) * INITIAL_SIZE)
    return vec
}

def extend(vec: *Vector, delta: int) {
    if (@vec).length + delta < (@vec).allocated {
        return
    }
    let nelem = max(roundup((@vec).length + delta), INITIAL_SIZE) !int
    (@vec).data = reallocate((@vec).data, (size_of type *) * nelem)
    (@vec).allocated = nelem
}

export def length(vec: *Vector) -> int {
    return (@vec).length
}

export def get(vec: *Vector, index: int) -> * {
    assert(index >= 0 and index < (@vec).length)
    return (@vec).data[index]
}

export def set(vec: *Vector, index: int, val: *) {
    assert(index >= 0 and index < (@vec).length)
    (@vec).data[index] = val
}

export def push(vec: *Vector, elem: *) {
    extend(vec, 1)
    (@vec).data[(@vec).length] = elem
    (@vec).length += 1
}

