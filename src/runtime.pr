type TypeKind = enum {
    BOOL
    WORD
    FLOAT
    STRUCT
    UNION
    ARRAY
    STATIC_ARRAY
    POINTER
    REFERENCE
    FUNCTION
    ENUM
}

type Type = struct {
    kind: TypeKind
    name: string
    unsig: bool
    length: size_t
    tpe: *Type
    size: size_t
    align: size_t
    fields: [Field]
    parameters: [Parameter]
    returns: [Type]
    enum_values: [EnumValue]
}

type EnumValue = struct {
    name: string
    value: int64
}

type Parameter = struct {
    name: string
    tpe: *Type
}

type Field = struct {
    name: string
    offset: int
    tpe: *Type
}

type Ref = struct {
    ref_count: *int64
    value: *
    tpe: *Type
}

def ref_type(a: Ref) -> *Type {
    return a.tpe
}

def equals(a: *Type, b: *Type) -> bool {
    if a == b { return true }
    if a.kind != b.kind { return false }
    if a.kind == TypeKind::BOOL { return true }
    if a.kind == TypeKind::WORD {
        return a.size == b.size and a.unsig == b.unsig
    }
    if a.kind == TypeKind::FLOAT {
        return a.size == b.size
    }
    if a.kind == TypeKind::STATIC_ARRAY {
        return equals(a.tpe, b.tpe) and a.length == b.length
    }
    if a.kind == TypeKind::ARRAY or a.kind == TypeKind::POINTER or a.kind == TypeKind::REFERENCE {
        return equals(a.tpe, b.tpe)
    }
    if a.kind == TypeKind::FUNCTION {
        if a.parameters.size != b.parameters.size { return false }
        if a.returns.size != b.returns.size { return false }
        for var i in 0..a.parameters.size {
            if not equals(a.parameters[i].tpe, b.parameters[i].tpe) { return false }
        }
        for var i in 0..a.returns.size {
            if not equals(*a.returns[i], *b.returns[i]) { return false }
        }
    }

    return a.name == b.name
}